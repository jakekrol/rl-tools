#!/usr/bin/perl -w
use strict;
use Getopt::Long;
use File::Basename;
use Statistics::Descriptive;
my $prog = basename($0);

sub print_usage {
	my ($msg) = @_;
    warn <<"EOF";

$msg

USAGE
  $prog [options]

DESCRIPTION
  Monte Carlo simulation
	
OPTIONS
  -h                  Print this help message
  --file  name        File we want use
  --val   col_num     Column of value (default 2)
  --I     interations Number of iterations
  --N     number      Size of random set
  --O     observed    Number observed over threshold  
  --T     threshold   Threshold value
  --sep   pattern     Charater seperating fields (defualt <tab>)

EOF
	exit;
}

my $file;
my $val_col = 2;
my $sep = "\t";
my $N;
my $O;
my $T;
my $I;
my $help = 0;

GetOptions ("file=s"	=> \$file,		# string
			"val=i"		=> \$val_col,
			"I=i"		=> \$I,
			"O=i"		=> \$O,
			"T=s"		=> \$T,
			"N=i"		=> \$N,
			"sep=s"		=> \$sep,
			"h"			=> \$help) or print_usage(); 

print_usage() if $help;

print_usage("No file") if not($file);
print_usage("No I") if not($I);
print_usage("No O") if not($O);
print_usage("No T") if not($T);
print_usage("No N") if not($N);

#### zero base cols
$val_col--;

open(FILE, $file) or die "Could not open $file.\n$!";

my @vals;

while (my $l = <FILE>) {
	chomp($l);
	my @a = split ($sep, $l);
	push(@vals, $a[$val_col]);
}

close(FILE);

my $size = @vals;

my $count = 0;
my @R;

for (my $i = 0; $i < $I; $i++){
	my $c = 0;
	for (my $j = 0; $j < $N; $j++){
		my $r = int(rand($size));
		if ($vals[$r] >= $T) {
			$c++;
		}
	}

	push(@R, $c);

	if ($c >= $O) {
		$count++;
	}
}
my $stat = Statistics::Descriptive::Full->new();
$stat->add_data(@R);
my $mean = $stat->mean();
my $stdev = $stat->standard_deviation();

my $p =( ($count+1) / ($I + 1) );

print join("\t", "I:". $I,
				 "O:" . $O,
				 "R:" . $count,
				 "mean:" . $mean,
				 "stdev:" . $stdev,
				 "p:" . $p) 
		. "\n";


